////////////////////////////////////////////////////////////////////////////////////////////
//            (C) Configuration Towards an Urban Preview - 2UP 2021                       //
//         Netherlands Environmental Assessment Agency http://www.pbl.nl/en/contact       //
//  This work is licensed under a Creative Commons Attribution 4.0 International License  //
//              CC BY-SA 4.0 https://creativecommons.org/licenses/by-sa/4.0/              //
////////////////////////////////////////////////////////////////////////////////////////////

// Author:   Bas van Bemmel
// Function:

container Suitability : using = "Units"
{
	container TRI
	{
		unit<uint32> Class
		:	StorageName     = "%projdir%/data/Suitability/TRI.csv"
		,	StorageType     = "gdal.vect"
		,	StorageReadOnly = "True"
		{
			attribute<Float32> RelOccurence := Float32(Probability), IntegrityCheck = "!IsDefined(strpos(Probability, ','))";
			attribute<string>  name         := 'TRI'+string(TRI);
			
			container V := for_each_nedv(name, 'value('+string(ID(.))+',..)', void, .);
		}

		container Data := for_each_nedv(Class/name, 'Input/Physical/TerrainRoughnessIndex/'+Class/name+'/ReadData', Input/gtopo, uint8);

		attribute<TRIweight> Weight(Input/gtopo) := ='add('+AsItemList('Float32(Data/'+Class/name+') * Class/RelOccurence[Class/V/'+Class/Name+']')+')';
	}

	container DistanceBuildUp
	{
		unit<uint32> Class
		:  	StorageName = "%projdir%/data/Suitability/DistanceUrbanArea.csv"
		,   StorageType = "gdal.vect";
	}

	container TravelTime
	{
		unit<uint8> Class
		:  	StorageName     = "%projdir%/data/Suitability/TravelTime.csv"
		,   StorageType     = "gdal.vect"
		,	StorageReadOnly = "True"
		{
			attribute<Float32> RelOccurence := Float32(Probability), IntegrityCheck = "!IsDefined(strpos(Probability, ','))";
		}
	
		attribute<float32> Weight (Input/gtopo) := MakeDefined(lookup(Input/Socioeconomic/TravelTime/TravelTimeGRIP/ReadData, Class/RelOccurence), 0.0f);
	}
	
	container Distance_UrbanArea1990
	{
		attribute<float32> UrbanPot (Input/gtopo) :
		= potential(float32(/Preprocessing/urban/BuiltUpBoolJRC_50prc/Y1990), Geography/Distmatrices/Impl/pot10km/potrange/RelWeightCorr);
	}

	container Distance_UrbanArea2010
	{
		attribute<float32> UrbanPot (Input/gtopo) :
		= potential(float32(Input/UrbanArea/UrbanArea2010/ReadData), Geography/Distmatrices/Impl/pot10km/potrange/RelWeightCorr);
	}

	container Coast
	{
		attribute<Bool> InRegion (Input/gtopo): = IsDefined(Input/administrative/Countries/ClaimCountry_grid);

		attribute<float32> buffer_25km (Input/gtopo):
			= !InRegion ? 0f : potential(float32(Input/Physical/Coast/Coastline/ReadData), float32(/Input/Geography/Distmatrices/Impl/pot25km/potrange/buffer));

		attribute<bool> Zone (Input/gtopo) := buffer_25km > 0f;
		
		container Potential
		{
			attribute<float32> pot_UrbanArea_5km (Input/gtopo):
			= !InRegion ? 0f : potential(float32(Input/UrbanArea/UrbanArea2010/ReadData), /Input/Geography/Distmatrices/Impl/pot5km/potrange/RelWeightSqrt)
			,cdf = "classifications/Input/Suitability/CoastPotClass/ClassBreak";

			attribute<float32> pot_CoastLine_20km (Input/gtopo) :=
			!InRegion ? 0f : potential(float32(Input/Physical/Coast/Coastline/ReadData), /Input/Geography/Distmatrices/Impl/pot20km/potrange/RelWeightSqrt)
			,	cdf = "classifications/Input/Suitability/CoastPotClass/ClassBreak";

			attribute<float32> UrbanCoastPot (Input/gtopo):
			= (pot_CoastLine_20km * pot_UrbanArea_5km) * 100f;

			//rescaled version
			attribute<float32> combination_UrbanCoast (Input/gtopo):
			= rescale((pot_CoastLine_20km * pot_UrbanArea_5km) * 100f, 0.0f, 100f);
		}
		
	}
}
