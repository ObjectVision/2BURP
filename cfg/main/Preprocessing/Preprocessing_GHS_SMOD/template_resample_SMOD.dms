////////////////////////////////////////////////////////////////////////////////////////////
//            (C) Configuration Towards an Urban Preview - 2UP 2021                       //
//         Netherlands Environmental Assessment Agency http://www.pbl.nl/en/contact       //
//  This work is licensed under a Creative Commons Attribution 4.0 International License  //
//              CC BY-SA 4.0 https://creativecommons.org/licenses/by-sa/4.0/              //
////////////////////////////////////////////////////////////////////////////////////////////

//Author:   Bas van Bemmel
//Function: Reproject GHSMOD from Mollweide to WGS84 (gtopo), result used for M3 var2
// Syntax:   https://www.geodms.nl/Mapping, https://www.geodms.nl/Mean, https://www.geodms.nl/Iif

template resample
{
	// template parameters	
	parameter<String> NameFile;
	// end case parameters
	parameter<String> OnlyNameFile:=right(NameFile, 47);
	
	unit<DPoint> WorldMollweide := /Input/WorldMollweide;

		unit<wpoint> JRC_GHS
		:	StorageName     = "=NameFile"
		,	StorageType     = "gdal.grid"
		,	StorageReadonly = "True"
		,	DialogData      = "WorldMollweide"
		{
			attribute<float32> GridData;

			//21:SUBURBAN OR PERI-URBAN GRID CELL
			//22:SEMI-DENSE URBAN CLUSTER GRID CELL
			//23'DENSE URBAN CLUSTER GRID CELL
			//30'URBAN CENTRE GRID CELL

			attribute<float32> UrbanGridCell :=GridData>=21f ? 1.0f : 0.0f;

			attribute<float32> URBANCENTREGRIDCELL30           :=GridData=30f ? 1.0f : 0.0f;
			attribute<float32> DENSEURBANCLUSTERGRIDCELL23     :=GridData=23f ? 1.0f : 0.0f;
			attribute<float32> SEMIDENSEURBANCLUSTERGRIDCELL22 :=GridData=22f ? 1.0f : 0.0f;
			attribute<float32> SUBURBANORPERIURBANGRIDCELL21   :=GridData=21f ? 1.0f : 0.0f;
			attribute<float32> RURALCLUSTERGRIDCELL13          :=GridData=13f ? 1.0f : 0.0f;
			attribute<float32> LOWDENSITYRURALGRIDCELL12       :=GridData=12f ? 1.0f : 0.0f;
			attribute<float32> VERYLOWDENSITYRURALGRIDCELL11   :=GridData=11f ? 1.0f : 0.0f;
			attribute<float32> WATERGRIDCELL10                 :=GridData=10f ? 1.0f : 0.0f;

			attribute<.> per_gtopo_sub(domain_sub) := mapping(domain_sub, .);
			attribute<uint64> nr_sub := pcount(per_gtopo_sub);

			attribute<Float32> var_per_gtopo_30 (domain) := mean(URBANCENTREGRIDCELL30[per_gtopo_sub],           domain_sub/domain_rel);
			attribute<Float32> var_per_gtopo_23 (domain) := mean(DENSEURBANCLUSTERGRIDCELL23[per_gtopo_sub],     domain_sub/domain_rel);
			attribute<Float32> var_per_gtopo_22 (domain) := mean(SEMIDENSEURBANCLUSTERGRIDCELL22[per_gtopo_sub], domain_sub/domain_rel);
			attribute<Float32> var_per_gtopo_21 (domain) := mean(SUBURBANORPERIURBANGRIDCELL21[per_gtopo_sub],   domain_sub/domain_rel);

			attribute<Float32> var_per_gtopo_13 (domain) := mean(RURALCLUSTERGRIDCELL13[per_gtopo_sub],          domain_sub/domain_rel);
			attribute<Float32> var_per_gtopo_12 (domain) := mean(LOWDENSITYRURALGRIDCELL12[per_gtopo_sub],       domain_sub/domain_rel);
			attribute<Float32> var_per_gtopo_11 (domain) := mean(VERYLOWDENSITYRURALGRIDCELL11[per_gtopo_sub],   domain_sub/domain_rel);
			attribute<Float32> var_per_gtopo_10 (domain) := mean(WATERGRIDCELL10[per_gtopo_sub],                 domain_sub/domain_rel);

			attribute<Float32> var_per_gtopo (domain)    := mean(UrbanGridCell[per_gtopo_sub],                    domain_sub/domain_rel);

			attribute<bool>    urban (domain)         := var_per_gtopo >0f ? 1[bool] : 0[bool],	StorageName = "='%LocalDataProjDir%/Results/'+OnlyNameFile+'_21_22_23_30_WGS84.tif'";
			
			attribute<Units/SMOD>    urban_30 (domain)         := var_per_gtopo_30 >0f ? 30[Units/SMOD] : 0u;
			attribute<Units/SMOD>    urban_23 (domain)         := var_per_gtopo_23 >0f ? 23[Units/SMOD] : 0u;
			attribute<Units/SMOD>    urban_22 (domain)         := var_per_gtopo_22 >0f ? 22[Units/SMOD] : 0u;
			attribute<Units/SMOD>    urban_21 (domain)         := var_per_gtopo_21 >0f ? 21[Units/SMOD] : 0u;
			
			attribute<Units/SMOD>    rural_13 (domain)         := var_per_gtopo_13 >0f ? 13[Units/SMOD] : 0u;
			attribute<Units/SMOD>    rural_12 (domain)         := var_per_gtopo_12 >0f ? 12[Units/SMOD] : 0u;
			attribute<Units/SMOD>    rural_11 (domain)         := var_per_gtopo_11 >0f ? 11[Units/SMOD] : 0u;
			attribute<Units/SMOD>    water_10 (domain)         := var_per_gtopo_10 >0f ? 10[Units/SMOD] : 0u;

			attribute<Units/SMOD>    SMODgtopo (domain)    := urban_30 =30u ? 
																	urban_30 : (urban_23 =23u ? 
																	urban_23 : (urban_22 =22u ? 
																	urban_22 : (urban_21 =21u ? 
																	urban_21 : (rural_13 =13u ? 
																	rural_13 : (rural_12 =12u ? 
																	rural_12 : (rural_11 =11u ? 
																	rural_11 : (water_10 =10u ? 
																	water_10 : 0u)))))));

			attribute<bool> rural (domain) := !urban;
		}
}