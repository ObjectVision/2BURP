container GDP : using = "units;sourcedata/regionalunits"
{
	parameter<string> GetLatLong := PropValue(LatLong, 'expr');
	parameter<string> GridName   := substr(GetLatLong, (strrpos(GetLatLong, '/'))+1, 40);

	container Data
	{
		unit<uint16> GDPregions
		: StorageName     = "%ToBURPDataDir%/GDP/GDPCap_Region_v5.shp"
		, StorageType     = "gdal.vect"
		, url             = "%ToBURPDataDir%/GDP/GDPregion_grid_v5.txt"
		, StorageReadOnly = "True"
		{
			attribute<uint16>   ID          := id(.);
			attribute<LatLong> Geometry(polygon);
			attribute<LatLong_mdegrees> Geometry_mdegrees       (polygon) := Geometry[LatLong_mdegrees];
			attribute<LatLong_mdegrees> Geometry_mdegrees_clean (polygon) := partitioned_union_polygon(Geometry_mdegrees,id(.));

			attribute<string> Region;
			attribute<string> Name       := Region;
			attribute<string> Label      := Name;
			attribute<uint32> grid_count := pcount(GDPregion_grid);
			attribute<float32> area      := sum(domain/lat_factor, GDPregion_grid);

			//Join to ISO3 produces the same result: the source file used is apparently the same -> GADM
			attribute<Countries/Country > Region_Country_rel := rlookup(NAME_0 ,Countries/Country/name);
			attribute<USD> REGGDPCAP;
			attribute<kUSD> REGGDPCAPkUSD:=REGGDPCAP[kUSD];
		}

		unit<uint16> TopoRegions := GDPregions
		{
			attribute<LatLong> Geometry(polygon) := GDPregions/Geometry;
			attribute<UInt32> BrushColor := const(0/0, .), DialogType = "BrushColor";
		}

		unit<uint16> EmptyRegions := subset(!GDP/Data/GDPregions/REGGDPCAP > 0f)
		{
			attribute<string> name                                        := GDP/Data/GDPregions/NAME_1[Nr_OrgEntity];
			attribute<Countries/Country > Region_Country_rel := GDP/Data/GDPregions/Region_Country_rel[Nr_OrgEntity];
		}

		unit<uint32> single_polygons := split_polygon(GDPregions/Geometry_mdegrees_clean)
		{
			attribute<LatLong_mdegrees>    Geometry (poly)
			{
				parameter<uint32>                PenColor      := rgb(255,0,0),DialogType = "PenColor";
				parameter<float64>               PenWorldWidth := 0.00001[float64],  DialogType = "PenWorldWidth";
			}
		}
	}

	attribute<uint16>  GDPregion_grid (domain)          := poly2grid(Data/GDPregions/Geometry , domain);

	attribute<bool>    IsGDPregion_grid (domain)    := GDPregion_grid[int16] > -1s;
	attribute<bool>    IsNotGDPregion_grid (domain) := !IsGDPregion_grid;

	unit<uint8> OverlayRegiosAll: nrofrows = 2
	{
		attribute<string> names: ['countryall', 'regionall'];
	}
	container OverlayGridsAll
	{
		attribute<uint16> countryall (domain):= SourceData/RegionalUnits/Countries/Country_grid[uint16];
		attribute<uint16> regionall (domain):= GDPregion_grid;
	}
	unit<uint32> crcombineAll:= overlay32(OverlayRegiosAll/names, domain, OverlayGridsAll);
	
	
	unit<uint8> OverlayRegios: nrofrows = 2
	{
		attribute<string> names: ['country', 'region'];
	}
	container OverlayGrids
	{
		attribute<uint2> country (domain):= (SourceData/RegionalUnits/Countries/Country_grid >= 0b ? true : false)[uint2];
		attribute<uint2> region (domain):= (GDPregion_grid >= 0w ? true : false)[uint2];
	}
	unit<uint32> crcombine:= overlay32(OverlayRegios/names, domain, OverlayGrids);

	#include<CreateIsCountry.dms>
	#include<GDP_Claims.dms>
	#include<GDP_Results.dms>
}