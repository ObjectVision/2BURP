//---------Input Urban Population Density (km2) over time---------------------------//
container HYDEPast
{
	container Import
	{
		unit<uint32> SSPPast
		: StorageName = "%projdir%/data/HYDE_PAST/HYDE_SSP2_popdens_1990_2015_INDEX1.csv"
		, StorageType = "gdal.vect"
		, StorageReadonly = "True"
		{
			attribute<SourceData/RegionalUnits/Countries> Country_rel := rlookup(Region, SourceData/RegionalUnits/Countries/ISO3);
			attribute<LatLong> geometry (polygon) := SourceData/RegionalUnits/Countries/Geometry_LL[Country_rel];
			attribute<string> Label := Region;

			parameter<string> Name := 'SSPPast';
		}
	}

//  Function to calculate total and additional urban area for the projection dates (HYDE index) based on the HYDE average urban density figures.
	container HYDEindex
	{
		unit<uint8> SSP: nrofrows = 1
		{
			attribute<string>  Name: ['SSPPast'];
			attribute<string>  Label := Name;
			container V := for_each_nedv(Name, 'value('+string(ID(.))+',..)', void, .);
		}

		container SSPs := 
			for_each_ne(SSP/Name, 
				'HYDEscenarios_Past_T(Import/'+SSP/Name+')'
				);

		template HYDEscenarios_Past_T
		{
			container HYDEclaim;
			parameter<string> Scenario:= HYDEclaim/Name;
			// parameter<string> PopulationInput := Modelparameters/Method;

			unit<uint8> ProjectionDate: nrofrows = 3
			{
				attribute<Year> Yr: [1990, 2000, 2015];
				attribute<string> name := 'Y'+string(Yr);
			}

			container Start 
			{
				container NewState
				{
					attribute<km2> UrbanArea (SourceData/RegionalUnits/Countries) := SourceData/Population/M1/Urban_pop_2010/UrbanArea;
				}
			}

			container Indices :=
				for_each_nedv(ProjectionDate/name,
					replace(
							'Import/@SSP@/_@YR@[float32][invert(Import/@SSP@/Country_rel)] '
								'/ Import/@SSP@/_1990[float32][invert(Import/@SSP@/Country_rel)]'
					,	'@SSP@', Scenario
					,	'@YR@', string(ProjectionDate/Yr)
					)
				,	SourceData/RegionalUnits/Countries , float32
				);

			container Evaluation
			{
				attribute<float32> Diff_2015_1990 (SourceData/RegionalUnits/Countries ) := Indices/Y2015 - Indices/Y1990;
			}
		}
	}
}