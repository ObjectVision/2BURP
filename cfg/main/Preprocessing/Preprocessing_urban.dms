container Urban : using = "geography;sourcedata/regionalunits"
{
	template UrbanAreaJRC_T 
	{
		// template parameters
		parameter<string> FileName;
		// end case parameters

		unit<ipoint> big_raster
		:	StorageName     = "= FileName+'.tif'"
		,	StorageType     = "gdal.grid"
		,	url             = "%ToBURPDataDir%/Preprocessing/urban/JRC/BuiltUpPresence/2016/Global_Human_Settlement_GHS_BUILT_UP_GRID_European_Commission.htm"
		,	Source          = "JRC"
		,	StorageReadOnly = "True"
		,	DialogData      = "SphericalMercator"
		{
			attribute<uint8> GridData; // don't read this; memory will explode
			attribute<bool>  Build_bool := GridData=101b;
			unit<ipoint> tiled_big_raster := TiledUnit(point(10240i, 10240i, big_raster)) 
			{
				attribute<uint8> GridData;
			}
			
			unit<uint64> urban_cells := select_orgrel(tiled_big_raster/GridData > 1b), DialogType = "map", DialogData = "org_rel"
			{
				attribute<uint8>  PercUrbanPlusOne := select_data(., tiled_big_raster/GridData);
				attribute<domain> domain_rel       := convert(org_rel, domain);
			}
		}
		
		unit<int32> rows := range(int32, PointRow(LowerBound(big_raster)), PointRow(UpperBound(big_raster))) 
		{
			attribute<big_raster> example_cell := point(ID(rows), const(first(ID(cols)), rows), big_raster);
			attribute<domain> domain_rel := example_cell[domain];
		}
		unit<int32> cols := range(int32, PointCol(LowerBound(big_raster)), PointCol(UpperBound(big_raster))) 
		{
			attribute<big_raster> example_cell := point(const(first(ID(rows)), cols), ID(cols), big_raster);
			attribute<domain> domain_rel := example_cell[domain];
		}
		unit<uint16> domain_rows := range(uint16, PointRow(LowerBound(domain)), PointRow(UpperBound(domain))) 
		{
			attribute<uint32> big_raster_count := pcount(pointrow(rows/domain_rel)[.]);
		}
		unit<uint16> domain_cols := range(uint16, PointCol(LowerBound(domain)), PointCol(UpperBound(domain))) 
		{
			attribute<uint32> big_raster_count := pcount(pointcol(cols/domain_rel)[.]);
		}

		attribute<uint32> TotalGridData      (domain) := sum(uint32(big_raster/urban_cells/PercUrbanPlusOne-1b), big_raster/urban_cells/domain_rel);
		attribute<uint32> TotalGridData_TEST1(domain) := sum_uint32(big_raster/urban_cells/PercUrbanPlusOne-1b, big_raster/urban_cells/domain_rel);
		attribute<uint32> TotalGridData_TEST2(domain) := sum_uint32(big_raster/urban_cells/PercUrbanPlusOne, big_raster/urban_cells/domain_rel)-uint32(pcount(big_raster/urban_cells/domain_rel));
		attribute<uint32> TotalGridData_TEST3(domain) := sum_uint32(big_raster/urban_cells/PercUrbanPlusOne, big_raster/urban_cells/domain_rel)-pcount_uint32(big_raster/urban_cells/domain_rel);
		attribute<uint16> LIGHT_cell_count   (domain) := uint16(domain_rows/big_raster_count)[pointrow(id(domain))] * uint16(domain_cols/big_raster_count)[pointcol(id(domain))];
	}

	template BuiltUpPresenceJRC_T 
	{
		parameter<uint16> Year;

		container P1 := UrbanAreaJRC_T('%ToBURPDataDir%/Preprocessing/urban/JRC/BuiltUpPresence/2016/GHS_BUILT_LDS'+string(Year)+'_GLOBE_R2016A_3857_38_v1_0_p1');
		container P2 := UrbanAreaJRC_T('%ToBURPDataDir%/Preprocessing/urban/JRC/BuiltUpPresence/2016/GHS_BUILT_LDS'+string(Year)+'_GLOBE_R2016A_3857_38_v1_0_p2');

		attribute<uint32> TotalGridData(domain) := max_elem(P1/TotalGridData, P2/TotalGridData);
		attribute<uint16> cell_count   (domain) := max_elem(P1/LIGHT_cell_count, P2/LIGHT_cell_count);
		attribute</Classifications/Prc/Prc_0_100>  urban_density(domain) := MakeDefined(uint8(TotalGridData / uint32(cell_count)), 0b)
			,	StorageName = "='%LocalDataProjDir%/Preprocessing/urban/JRC/BuiltUpPresence/2016/output/BuiltUpPresenceJRC_'+string(Year)+'.tif'";
	}

	unit<uint8> JrcDataset : nrofrows = 4 { attribute<uint16> year: [ 1975, 1990, 2000, 2014 ]; attribute<string> name := 'Y'+string(year); }

	container BuiltUpPresenceJRC_Create := for_each_ne(JrcDataset/name, 'BuiltUpPresenceJRC_T('+string(JrcDataset/year)+'w)');

	container BuiltUpPresenceJRC_Read := for_each_ndvat(JrcDataset/name, 
		domain, /Classifications/Prc/Prc_0_100, 
		'%ToBURPDataDir%/Preprocessing/urban/JRC/BuiltUpPresence/2016/output/BuiltUpPresenceJRC_'+string(JrcDataset/year)+'.tif', 'gdal.grid'),
		Source = "export van /Urban/UrbanAreaJRC_yyyy/LIGHT_urban_density in GeoDMS 7.159",
		url = "";

	//for postprocessing urban result
	attribute<float32> Builtuparea(domain)     := value((BuiltUpPresenceJRC_Read/Y2014),float32) / 100f;
	attribute<Units/km2> Builtupareakm2(domain)     := Builtuparea * domain/area;
	
	attribute<float32> Builtuparea1990(domain) := value((BuiltUpPresenceJRC_Read/Y1990),float32) / 100f;
	attribute<Units/km2> Builtupareakm2_1990(domain)     := Builtuparea1990 * domain/area;
	
	attribute<float32> Builtuparea2000(domain) := value((BuiltUpPresenceJRC_Read/Y2000),float32) / 100f;
	attribute<Units/km2> Builtupareakm2_2000(domain)     := Builtuparea2000 * domain/area;
	
	
	//classification of percentage, not used anywhere in the model, only visualisation
	container BuiltUpPresenceJRC_View := for_each_nedv(JrcDataset/name, 
		'/Classifications/Prc/Prc_0_100/Prc_0_100_K_rel[BuiltUpPresenceJRC_Read/'+JrcDataset/name+']',
		domain, /Classifications/Prc/Prc_0_100_K),
		Source = "export of /Urban/UrbanAreaJRC_yyyy/LIGHT_urban_density in GeoDMS 7.159",
		url = "";

	unit<uint32> Jaartal: nrofrows = 4
	{
		attribute<int16>  dc:    [1975,1990,2000,2014];
		attribute<int16>  dc2:   [1975,1990,2000,2015];
		attribute<String> name:= 'y'+String(dc);
		attribute<String> name2:='y'+String(dc2);
	}

	//these items are used for the modelling
	container BuiltUpBoolJRC_50prc: =
		for_each_nedvd(
			 Jaartal/name
			  ,'BuiltUpPresenceJRC_Read/'+ Jaartal/name + '> 50b'
			,  domain
			,  bool
			,  'Urban area 50percent in past year ' + String(Jaartal/dc)
		);
		
	container BuiltUpBoolJRC_50prc_km2: =
		for_each_nedvd(
			 Jaartal/name
			 ,'value(BuiltUpBoolJRC_50prc/'+ Jaartal/name + ',float32)* domain/area'
			,  domain
			,  Units/km2
			,  'Urban area in km2' + String(Jaartal/dc)
		);

	container NotBuiltUpBoolJRC_50prc: =
		for_each_nedvd(
			 Jaartal/name
			  ,'!BuiltUpBoolJRC_50prc/'+ Jaartal/name
			,  domain
			,  bool
			,  'Not Urban area 50percent in past year ' + String(Jaartal/dc)
		);

	container Urban_km2: =
		for_each_nedvd(
			 Jaartal/name
			  ,'BuiltUpBoolJRC_50prc/'+ Jaartal/name + '[float32] * domain/area'
			,  domain
			,  Units/km2
			,  'Urban km2 in past year ' + String(Jaartal/dc)
		);

	container BuiltUpSum: =
		for_each_nedvd(
			 Jaartal/name
			 ,'sum(value(BuiltUpBoolJRC_50prc/'+ Jaartal/name + ',uint32), Countries/Country_grid)'
			,  Countries/Country 
			,  uint32
			,  'Sum Urban area ' + String(Jaartal/dc)
		);

	container BuiltUpSum_km2: =
		for_each_nedvd(
			 Jaartal/name2
			 ,'sum(value(BuiltUpBoolJRC_50prc_km2/'+ Jaartal/name + ',Units/km2), Countries/Country_grid)'
			,  Countries/Country 
			,  Units/km2
			,  'Sum Urban area in km2 ' + String(Jaartal/dc)
		);

	container BuiltUpSum_km2_1990: =
		for_each_nedvd(
			 Jaartal/name2
			 ,'BuiltUpSum_km2/Y1990'
			,  Countries/Country 
			,  Units/km2
			,  'Sum Urban area in km2 ' + String(Jaartal/dc)
		);
}