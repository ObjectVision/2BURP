container Impl 
{
	parameter<float64> SourceRasterSubFactor      := 5.0 / 3600.0;
	parameter<float64> SourceRasterFactor         := 0.00833333333333333333333333333333333333333333333333333333333;
	unit<ipoint>       LatLong_mdegrees           := gridset(LatLong, point_yx(0.0000001, 0.0000001), point_yx(0.0,0.0), ipoint);
	parameter<float64> SourceRasterFactor_in_eq_m := SourceRasterFactor * (40000000.0 / 360.0);
		
	unit<wpoint> ProtoDomain :=
		gridset(
			range(LatLong_Base, point_yx(-90.0,-180.0), point_yx(90.0, 180.0))
			, point_yx(-SourceRasterFactor, SourceRasterFactor, LatLong_base) 
			, point_yx(90d, -180d, LatLong_base) //top left
			, wpoint
		);
		
	unit<ipoint> ProtoDomain_sub :=
		gridset(
			range(LatLong_Base, point_yx(-90.0,-180.0), point_yx(90.0, 180.0))
			, point_yx(-SourceRasterSubFactor, SourceRasterSubFactor, LatLong_base) 
			, point_yx(90d, -180d, LatLong_base) //top left
			, ipoint
		);
		
	parameter<ProtoDomain> TileSize   := Point_yx(1024w, 1024w, ProtoDomain);
	parameter<ProtoDomain> TileOffset := Point_yx(   0w,    0w, ProtoDomain);
	
	unit<spoint>  Tile := gridset(ProtoDomain, TileSize, TileOffset, spoint)
	{
		attribute<bool>         LatLong_per_ProtoDomain (ProtoDomain) := poly2grid(LatLong/geometry, ProtoDomain);
		unit<uint32>            PD_StudyAreaSelection                 := select_with_org_rel(LatLong_per_ProtoDomain);
		attribute<ProtoDomain>  LB                                    := min(PD_StudyAreaSelection/org_rel, PD_StudyAreaSelection/org_rel[.]);
		attribute<ProtoDomain>  UB                                    := max(PD_StudyAreaSelection/org_rel, PD_StudyAreaSelection/org_rel[.])+point_yx(1w, 1w);
		attribute<bool>         HasStudyArea                          := any(LatLong_per_ProtoDomain, ProtoDomain_rel);
		
		attribute<.>            ProtoDomain_rel         (ProtoDomain) := mapping(ProtoDomain, .);
	}

	unit<uint32> StudyAreaTile_Select := select_with_org_rel(Tile/HasStudyArea)
	{
		attribute<ProtoDomain>      LB     := Tile/LB[org_rel];
		attribute<ProtoDomain>      UB     := Tile/UB[org_rel];
		attribute<ProtoDomain_sub>  LB_sub := LB[ProtoDomain_sub];
		attribute<ProtoDomain_sub>  UB_sub := UB[ProtoDomain_sub];
	}

	unit<uint32> StudyAreaTile_Create := StudyAreaTile_Select, StorageName = "='%projdir%/data/StudyAreaTiles_'+ModelParameters/StudyArea+'.dbf'" // decouple by renaming this file when produced
	{
		attribute<uint16>           LB_X := pointcol(LB);
		attribute<uint16>           LB_y := pointrow(LB);
		attribute<uint16>           UB_X := pointcol(UB);
		attribute<uint16>           UB_y := pointrow(UB);
		
		attribute<int32>            LB_sub_X := pointcol(LB_sub);
		attribute<int32>            LB_sub_y := pointrow(LB_sub);
		attribute<int32>            UB_sub_X := pointcol(UB_sub);
		attribute<int32>            UB_sub_y := pointrow(UB_sub);
	} 

	// from here on, we only use the decoupled study area tiles and no longer have to process the full extent of the study area.
	unit<uint32> StudyAreaTile: StorageName = "='%projdir%/data/StudyAreaTiles_'+ModelParameters/StudyArea+'.dbf'", StorageReadOnly = "true"
	{
		attribute<int32>            LB_X;
		attribute<int32>            LB_y;
		attribute<int32>            UB_X;
		attribute<int32>            UB_y;
		attribute<int32>            LB_sub_X;
		attribute<int32>            LB_sub_y;
		attribute<int32>            UB_sub_X;
		attribute<int32>            UB_sub_y;
		
		attribute<ProtoDomain>      LB    := point_yx(uint16(LB_y), uint16(LB_x), ProtoDomain);
		attribute<ProtoDomain>      UB    := point_yx(uint16(UB_y), uint16(UB_x), ProtoDomain);
		
		attribute<ProtoDomain_sub>  LB_sub    := point_yx(LB_sub_y, LB_sub_x, ProtoDomain_sub);
		attribute<ProtoDomain_sub>  UB_sub    := point_yx(UB_sub_y, UB_sub_x, ProtoDomain_sub);
	} 

	container MakeDomain_perContinent :=
		for_each_ne(
			SourceData/RegionalUnits/Continents/name
			, 'MakeDomain_perStudyArea_T('+quote(SourceData/RegionalUnits/Continents/name)+')'
		);
	
	Template MakeDomain_perStudyArea_T
	{
		parameter<string> StudyArea_name;
		///
		
		unit<uint32> StudyAreaTile: StorageName = "='%projdir%/data/StudyAreaTiles_'+StudyArea_name+'.dbf'", StorageReadOnly = "true"
		{
			attribute<int32>            LB_X;
			attribute<int32>            LB_y;
			attribute<int32>            UB_X;
			attribute<int32>            UB_y;
			attribute<int32>            LB_sub_X;
			attribute<int32>            LB_sub_y;
			attribute<int32>            UB_sub_X;
			attribute<int32>            UB_sub_y;
			attribute<ProtoDomain>      LB    := point_yx(uint16(LB_y), uint16(LB_x), ProtoDomain);
			attribute<ProtoDomain>      UB    := point_yx(uint16(UB_y), uint16(UB_x), ProtoDomain);
			attribute<ProtoDomain_sub>  LB_sub    := point_yx(LB_sub_y, LB_sub_x, ProtoDomain_sub);
			attribute<ProtoDomain_sub>  UB_sub    := point_yx(UB_sub_y, UB_sub_x, ProtoDomain_sub);
		} 

		unit<wpoint> domain := TiledUnit(StudyAreaTile/LB, StudyAreaTile/UB);
	}

	container GridData : using = "SourceData/RegionalUnits"
	{
		attribute<Person> InitialPopGrid         (domain) := ='SourceData/Population/PerYear/'+ModelParameters/StartYear+'[Person]';
		// attribute<Person> InitialPopGrid1990     (domain) := CompactedDomain/ISCD ? SourceData/Population/PerYear/Y1990[Person] : null_d[Person];
		attribute<Bool>   InStudyArea            (domain) := poly2grid(LatLong/geometry, domain);
		attribute<Bool>   HasPop                 (domain) := InitialPopGrid > 0[Person];
		attribute<Bool>   HasBuiltup             (domain) := ='IsDefined(Countries/per_domain) ? (SourceData/Builtup/Total/'+ModelParameters/StartYear+' > 0[m2]) : false';
		attribute<Bool>	  InBU_5kmClip           (domain) := ='IsDefined(Countries/per_domain) && (SourceData/Builtup/Total_sums_5km/'+ModelParameters/StartYear+' > 0[m2])';

		attribute<bool>   IsNotLake              (domain) := not(Physical/Water/Inland_Water/Read_per_grid);
		attribute<bool>   IsNotIce               (domain) := not(Physical/SnowIceCover/ReadData);

		attribute<Bool>   FreeLand               (domain) := InStudyArea                                // is it within the study area (e.g. country border)
																&& InBU_5kmClip                         // within certain distance from bu-area.
																&& (IsNotLake || HasBuiltup || HasPop)  // it is not a lake, unless there is builtup or population in a cell categorised as a lake
																&& (IsNotIce || HasBuiltup || HasPop);  // it is not ice, unless there is builtup or population in a cell categorised as a ice
	}

}