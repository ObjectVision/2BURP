Template Allocate_Population_T
{
	parameter<string>  ThisYear;
	parameter<string>  PrevYear;
	container          OldState_Pop; //fka CurrState
	container          OldState_BuiltUp;
	container          NewState_BuiltUp;
	attribute<Person>  Claim (ClaimRegion);
	///
	// attribute<float32> Initial_Population_Proportional_Reduction_Rate (ClaimRegion) := ='Preprocessing/PercDelta_Claims_Population/'+ThisYear+' < 0f 
																							// ? max_elem(abs(Preprocessing/PercDelta_Claims_Population/'+ThisYear+') * ModelParameters/Pop_Additional_Claim_Reduction_Factor, ModelParameters/Pop_Internal_Migration_Reduction_Rate_base) 
																							// : ModelParameters/Pop_Internal_Migration_Reduction_Rate_base';
	// attribute<float32> Initial_Population_Absolute_Reduction_Rate     (ClaimRegion) := ='Preprocessing/PercDelta_Claims_Population/'+ThisYear+' < 0f 
																							// ? max_elem(abs(Preprocessing/PercDelta_Claims_Population/'+ThisYear+') * ModelParameters/Pop_Additional_Claim_Reduction_Factor, ModelParameters/Pop_Initial_Absolute_Reduction_Rate_base) 
																							// : ModelParameters/Pop_Initial_Absolute_Reduction_Rate_base';
	attribute<float32> Initial_Population_Scaled_Reduction_Rate 	  (ClaimRegion) := ='abs(min_elem(Preprocessing/PercDelta_Claims_Population/'+ThisYear+', 0f)) + ModelParameters/Pop_Internal_Migration_Reduction_Rate_base'
																								, Descr = "per region the population decline rate (if present) plus the proportional reduction rate as a proxy for internal migration.";
	
	
	container LocalMaxima
	{
		attribute<Person_BuiltUpKm2>         Current_LocalMaximum  (CompactedDomain) := max(OldState_Pop/Population_inRes_Density[DistMatrices/cdomain_x_pot1k/third_rel], DistMatrices/cdomain_x_pot1k/first_rel); 
		attribute<bool>                      IsLocalMaximum        (CompactedDomain) := Current_LocalMaximum == OldState_Pop/Population_inRes_Density;
		attribute<bool>                      HasNoPopulation       (CompactedDomain) := OldState_Pop/Population == 0[NrPerson];
		
		// If a cell has the highest local population in T1, it can only increase by 3.5K people in T2 (or by [100K - local pop] if this is smaller than 3.5K people)
		// If a cell is populated but it doesnâ€™t have the highest local population in T1, it can only increase in T2 to the local maximum in T1 + 2.5K people (or by [100K - local pop] if this is smaller than the local maximum in T1 + 2.5K people) 
		// If a cell is not populated in T1, it can only increase by 350 people in T2
		attribute<Person_BuiltUpKm2>  MaxAllowed_Population_Density (CompactedDomain) := 
			iif(
				HasNoPopulation
				, ModelParameters/pop_r_if_notPopulated * 1[decade]
				// , median(ModelParameters/pop_max_saturation, OldState_Pop/Population_inRes_Density, OldState_Pop/Population_inRes_Density + (LocalMaxima/IsLocalMaximum ? ModelParameters/pop_r_if_LocalMax : ModelParameters/pop_r_if_notLocalMax)  * 1[decade])
				, median(
					  ModelParameters/pop_max_saturation
					, OldState_Pop/Population_inRes_Density
					, LocalMaxima/IsLocalMaximum
						? OldState_Pop/Population_inRes_Density + ModelParameters/pop_r_if_LocalMax  * 1[decade] 
						: Current_LocalMaximum + ModelParameters/pop_r_if_LocalMax  * 1[decade]
				)
			);
			
		attribute<Person>             MaxAllowed_Population         (CompactedDomain) := MaxAllowed_Population_Density * NewState_BuiltUp/BuiltUp_Residential_Area;
		// attribute<Person>             MaxAllowed_Population         (CompactedDomain) := MaxAllowed_Population_Density *  CompactedDomain/area;
	}
	
	container Claims
	{
		attribute<Person>            Population                         (ClaimRegion) := Claim;
	}
	
	container Suitability
	{
		container AutoGLM_Coeffs  := Analysis/Calibration/ReadCoffecients/coef2domain/domain;
		attribute<Suit> Calc (domain) := ='(OldState_Pop/PopulationDensity_8dir/Ln_mean_8dir[float32]                                                                         * AutoGLM_Coeffs/ln_PopulationDensity_2000_8dir) + '
																'(min_elem(Physical/Coast/Location2Coast_m_sqr/Result[float32],250f)                                          * AutoGLM_Coeffs/Distance2Coastline) + 
																(min_elem(Physical/Water/Location2LargeInlandWater_m_sqr/Result[float32],250f)                                * AutoGLM_Coeffs/Distance2LargeInlandWater) + 
																(min_elem(makedefined(Socioeconomic/Infrastructure/Location2Roads45_m_sqr/Result[float32],1000f),250f)        * AutoGLM_Coeffs/Distance2SecundaryRoads) + 
																(Calculate_GridDistances/'+PrevYear+'/Grid_costs_atleast_to/villages      		                              * AutoGLM_Coeffs/gc_villages) + 
																(Calculate_GridDistances/'+PrevYear+'/Grid_costs_atleast_to/cities                                      	  * AutoGLM_Coeffs/gc_cities) + 
																(Physical/Elevation/Mean_1k[float32]                                                                          * AutoGLM_Coeffs/Elevation) + 
																(Physical/Slope_2021/ReadData[float32]                                                                        * AutoGLM_Coeffs/Slope) + 
																(makedefined(Physical/TerrainRoughnessIndex/TRI_mean_30ss_World[float32], 0f)                                 * AutoGLM_Coeffs/TRI_mean) + 
																(Policy/ProtectedAreas/ReadData[float32]                                                                      * AutoGLM_Coeffs/IsProtectedArea) + 
																(Physical/Water/FloodProneArea/RP100/ReadData_Defined[float32]                                                * AutoGLM_Coeffs/IsFloodProneArea_RP100) + 
																(Physical/Natural_Hazards/Earthquake/Earthquakes_Modified_Mercalli_Intensity/ReadData[float32]                * AutoGLM_Coeffs/Earthquakes_MMI_Index) + 
																(Physical/Natural_Hazards/Landslide/LS_risk[float32]                                                          * AutoGLM_Coeffs/Landslide_Prone) + 
																AutoGLM_Coeffs/Intercept';
																
		attribute<Suit> Write_Calc (domain) := Calc, StorageName = "='%LocalDataProjDir%/Temp/'+ModelParameters/StudyArea+'/Suitability_'+ModelParameters/Calibration_filedate+'_Pop_'+ThisYear+'_'+ModelParameters/domain_EPSG+'.tif'";
		attribute<Suit> Read_Calc  (domain) : StorageName = "='%LocalDataProjDir%/Temp/'+ModelParameters/StudyArea+'/Suitability_'+ModelParameters/Calibration_filedate+'_Pop_'+ThisYear+'_'+ModelParameters/domain_EPSG+'.tif'", StorageReadOnly = "True";
				
		attribute<Suit>              BuiltUp                        (CompactedDomain) := =ModelParameters/Use_TempTifFiles ? 'Read_Calc[CompactedDomain/org_rel]' : 'Write_Calc[CompactedDomain/org_rel]';
		attribute<Suit>              CappedBuiltUp                  (CompactedDomain) := min_elem(BuiltUp, 80[suit]), Descr = "";
		attribute<Suit>              expCappedBuiltUp               (CompactedDomain) := exp(CappedBuiltUp),  Descr = "";
		attribute<float32>           Prob                           (CompactedDomain) := expCappedBuiltUp / (1f+ expCappedBuiltUp), Descr = "Probability of built up at that location";
	
		attribute<Suit>              NonNegative                    (CompactedDomain) := BuiltUp > 0f ? BuiltUp + 1f : exp(BuiltUp), Descr = "Make the probability non-negatiev";
		attribute<Suit>              Population                     (CompactedDomain) := NonNegative, Descr = "Interpret the built-up probability as the suitability for population allocation";
		attribute<Suit>              Scaled_Reduction               (CompactedDomain) := (1f - ModelParameters/Pop_Suitability_Reduction_Scale) + (ModelParameters/Pop_Suitability_Reduction_Scale * min_elem(max_elem(1[Suit] - Prob, 0[Suit]), 1[Suit]))
																							, Descr = "Use the inverse suitability as a proxy to choose locations for population reduction scale with proportional reduction";
	}
	
	container NewState
	{
		//Reduction step 1: All cells will lose 10% of their initial population. This proportional reduction has the biggest impact in dense cells.
		//attribute<Person>            Proportional_Reducted_Population       (CompactedDomain) := OldState_Pop/Population[Person] * (1f - Initial_Population_Proportional_Reduction_Rate[ClaimRegion/per_CompactedDomain]); 
		
		//Reduction step 2: All inhabited cells will lose up to a fixed (per functional area) number of persons of 10% of the initial average population of the inhabited grid cells. This absolute reduction will have the biggest impact on low-density cells.
		attribute<NrPerson>          Population_in_Region                      (ClaimRegion) := sum(OldState_Pop/Population, Region_rel), Descr = "Sum of population in region";
		//attribute<uint32>            NumberOfCellsWithPopulationPerRegion       (ClaimRegion) := sum_uint32(OldState_Pop/Population > 0[nrPerson], Region_rel);
		//attribute<float32>           Average_Population_per_PopulatedCell       (ClaimRegion) := Population_in_Region[Person] / NumberOfCellsWithPopulationPerRegion[float32];
		//attribute<Person>            Regional_Reduction_per_cell                (ClaimRegion) := Average_Population_per_PopulatedCell * Initial_Population_Absolute_Reduction_Rate;
		//attribute<Person>            Reduced_Population                     (CompactedDomain) := max_elem(Proportional_Reducted_Population - Regional_Reduction_per_cell[Region_rel], 0[Person]);
		
		// Reduction scaled attempt: cells will lose 
		attribute<float32>           Regional_Population_to_draw                (ClaimRegion) := Population_in_Region[float32] * Initial_Population_Scaled_Reduction_Rate, Descr = "How many people need to drawn from the region. To accomodate regional decline (if present) and internal migration.";
		attribute<float32>           Scaled_Reducible_pop                   (CompactedDomain) := OldState_Pop/Population[float32] * Suitability/Scaled_Reduction[float32], Descr = "Initial population times the inverse suitability proxy to get the potential population taken out per cell.";
		attribute<float32>           Scaled_Reduction                       (CompactedDomain) := min_elem(scalesum(Scaled_Reducible_pop, Region_rel, Regional_Population_to_draw), OldState_Pop/Population[float32]), Descr = "Disaggregate the to-be-drawn population according to the inverse suitability proxy weigthed by the initial population. Canntot take out more than the present population though.";
		attribute<float32>			 Regional_Scaled_Reduction					(ClaimRegion) := sum(Scaled_Reduction, Region_rel), Descr = "Sum how many people are drawn from the existing population in the functional area";
		attribute<float32> 			 Proportion_Potential_Additional_Reduction	(ClaimRegion) := makedefined(max_elem(Regional_Population_to_draw - Regional_Scaled_Reduction, 0f) / (Population_in_Region[float32] - Regional_Scaled_Reduction), 0f), descr = "Calculate the percentage of people that are drawn proportionally if the scaled reduction does not yield sufficient people";
		attribute<float32>			 Additional_Proportional_Reduction		(CompactedDomain) := Proportion_Potential_Additional_Reduction[Region_rel] * (OldState_Pop/Population[float32] - Scaled_Reduction);
		
		// attribute<float32>           Relative_Reduction                     (CompactedDomain) := Scaled_Reduction / OldState_Pop/Population[float32], Descr "";
		attribute<float32>           Reduced_Population                     (CompactedDomain) := OldState_Pop/Population[float32] - Scaled_Reduction - Additional_Proportional_Reduction, IntegrityCheck = "PopLeftAfterReduction >= 0f || ClaimRegion/ignore_Check_for_enough_drawn_population", Descr = "Initial population minus the reduction due to decline and migration.";
		
		attribute<float32>           Population_to_reduce                       (ClaimRegion) := sum(Scaled_Reduction + Additional_Proportional_Reduction, Region_rel), Descr = "";
		attribute<float32>           PopLeftAfterReduction                      (ClaimRegion) := 1f + Population_to_reduce - Regional_Population_to_draw , Descr = "Integrity Check condition to ensure we do not reduce more people than are allowed to be drawn in the region.";
		
		
		//Adding step 3: Newly built-up areas will be populated based on the initial local average built-up population density in a 3x3 or 5x5 cell radius
		attribute<NrPerson>          Avg_Local_Population_inRes_Density_Num (CompactedDomain) := sum(OldState_Pop/Population[DistMatrices/cdomain_x_pot3k/third_rel], DistMatrices/cdomain_x_pot3k/first_rel), Descr = ""; 
		attribute<BuiltUpKm2>        Avg_Local_Population_inRes_Density_Den (CompactedDomain) := sum(BuiltUp_Residential_Area[DistMatrices/cdomain_x_pot3k/third_rel], DistMatrices/cdomain_x_pot3k/first_rel), Descr = ""; 
		attribute<Person_BuiltUpKm2> Avg_Local_Population_inRes_Density     (CompactedDomain) := Avg_Local_Population_inRes_Density_Num[Person] / Avg_Local_Population_inRes_Density_Den, Descr = ""; 
		attribute<BuiltUpKm2>        BuiltUp_Total_Area                     (CompactedDomain) := NewState_BuiltUp/BuiltUp_Total_Area, Descr = ""; 
		attribute<BuiltUpKm2>        BuiltUp_Residential_Area               (CompactedDomain) := NewState_BuiltUp/BuiltUp_Residential_Area, Descr = ""; 
		
		// definition in case total area is allocated
		//attribute<BuiltUpKm2>        New_BuiltUp_Area                       (CompactedDomain) := max_elem(BuiltUp_Total_Area - OldState_BuiltUp/BuiltUp_Total_Area, 0f); 
		
		// definition in case residential area is allocated
		attribute<BuiltUpKm2>        New_BuiltUp_Area                       (CompactedDomain) := max_elem(BuiltUp_Residential_Area - OldState_BuiltUp/BuiltUp_Residential_Area, 0f), Descr = ""; 
		attribute<Person>            Population_in_New_BuiltUp_Area_src     (CompactedDomain) := MakeDefined(New_BuiltUp_Area * Avg_Local_Population_inRes_Density, 0[Person]), Descr = "";
		
		attribute<Person>            Pop_in_New_BU_Area_Augm_Teller             (ClaimRegion) := max_elem((Claims/Population - sum(Reduced_Population, Region_rel)) * ModelParameters/Pop_ShareOfRestclaim_Prefers_newBU, 0[Person]), Descr = "Rest claim per regio that could be allocated to new BU area"; 
		attribute<Person>            Pop_in_New_BU_Area_Augm_Noemer             (ClaimRegion) := sum(Population_in_New_BuiltUp_Area_src, Region_rel), Descr = "Total possible new population in new BU per region";
		attribute<float32>           Pop_New_BU_PopulationDensity_Factor        (ClaimRegion) := min_elem(1f, Pop_in_New_BU_Area_Augm_Teller / Pop_in_New_BU_Area_Augm_Noemer), Descr = "";
		
		attribute<Person>            Population_in_New_BuiltUp_Area         (CompactedDomain) := Population_in_New_BuiltUp_Area_src * Pop_New_BU_PopulationDensity_Factor[Region_rel], Descr = "";

		//Adding step 4: To the population we add the difference between the maximum allowed density and the initial density, after scaling it by the suitability, augmented to meet functional area claims.
		attribute<bool>              Population_beforeAllocation_Check          (ClaimRegion) := sum(Reduced_Population + Population_in_New_BuiltUp_Area, Region_rel) <= Claims/Population + (Claims/Population * 0.01f), Descr = ""
		{
			attribute<Person> Reduced_Population_perRegion             (ClaimRegion) := sum(Reduced_Population, Region_rel), Descr = "";
			attribute<Person> Population_in_New_BuiltUp_Area_perRegion (ClaimRegion) := sum(Population_in_New_BuiltUp_Area, Region_rel), Descr = "";
		}
		attribute<Person>            Population_beforeAllocation            (CompactedDomain) := Reduced_Population + Population_in_New_BuiltUp_Area, IntegrityCheck = "Population_beforeAllocation_Check", Descr = "";
		attribute<Person>            Room_for_Population                    (CompactedDomain) := max_elem(LocalMaxima/MaxAllowed_Population - Population_beforeAllocation, 0f), Descr = "";
		attribute<float32>           ProportionalityFactor                  (CompactedDomain) := max_elem(Suitability/population * Room_for_Population, 0f), Descr = "";
		attribute<float32>           Sum_ProportionalityFactor                  (ClaimRegion) := sum(ProportionalityFactor , Region_rel), Descr = "";
		attribute<Person>            RestClaim                                  (ClaimRegion) := max_elem(Claims/Population - sum(Population_beforeAllocation, Region_rel), 0[Person]), Descr = "";
		attribute<float32>           ScaleFactor_0                              (ClaimRegion) := RestClaim / Sum_ProportionalityFactor, IntegrityCheck = "Check_for_enough_suitable_locations", Descr = ""; //Number of persons per suitability value. 
		
		attribute<float32>           Population_0                           (CompactedDomain) := ScaleFactor_0[Region_rel] * Suitability/Population, Descr = ""; //for debug purposes, not really number of persons!
		attribute<int32>             Sum_Room_for_population                    (ClaimRegion) := sum(Suitability/Population > 0f ? int32(Room_for_Population) : 0i, Region_rel), Descr = ""; //for debug purposes
		attribute<bool>              Check_for_enough_suitable_locations        (ClaimRegion) := ClaimRegion/ignore_check_enough_suitable_locations || Sum_Room_for_population[Person] >= RestClaim, Descr = ""; //for debug purposes
		
		unit<uint8> Iter := cat_range(0b,10b) 
		{
			attribute<string> name      := 'Iter_'+string(id(.));
			attribute<string> prev_name := MakeDefined(name[sub_or_null(id(.),1b)]+'/ScaleFactor_new', 'ScaleFactor_0');
		}
		
		container Population_Allocation :=
			for_each_ne(
				Iter/name
				, 'Population_Allocation_T('+Iter/prev_name+')'
			)
		{
			container Last := =last(Iter/name);
		}

		Template Population_Allocation_T
		{
			attribute<float32>       ScaleFactor                  (ClaimRegion);
			///
			attribute<Person>        Proportional_Population  (CompactedDomain) := ScaleFactor[Region_rel] * ProportionalityFactor;
			attribute<Person>        Saturated_Population     (CompactedDomain) := min_elem(Proportional_Population, Room_for_Population);
			attribute<Person>        Total_Population         (CompactedDomain) := Saturated_Population + Population_beforeAllocation;
			// attribute<NrPerson>      Total_Population_rounded (CompactedDomain) := int32(Total_Population)[NrPerson]; //we round here, to omit fractional people which can then reallocated in the next iter.
			// attribute<NrPerson>      Realisation                  (ClaimRegion) := sum(Total_Population_rounded, Region_rel);
			attribute<Person>        Realisation                  (ClaimRegion) := sum(Total_Population, Region_rel);
			attribute<float32>       RescaleFactor                (ClaimRegion) := Claims/Population / Realisation; // We need to correct the scale factor to accomodate left over claims.
			attribute<float32>       ScaleFactor_new              (ClaimRegion) := IsNull(ScaleFactor) || IsNull(RescaleFactor) ? 0f : ScaleFactor * RescaleFactor;
		}
		
		//Results  
		// attribute<NrPerson>          Population_LastIterResult    (CompactedDomain) := Population_Allocation/Last/Total_Population_rounded;
		attribute<Person>     Population_LastIterResult         (CompactedDomain) := Population_Allocation/Last/Total_Population;
		
		//Rounding
		attribute<NrPerson>   Population_Int                    (CompactedDomain) := Population_LastIterResult[NrPerson];
		attribute<float32>    Rest_Share                        (CompactedDomain) := Population_LastIterResult - float32(Population_Int);
		attribute<Person>     sum_rounded_pop                       (ClaimRegion) := sum(float32(Population_Int), Region_rel);
		attribute<uint32>     Rest_claim                            (ClaimRegion) := uint32(Claims/Population - sum_rounded_pop);
		attribute<float32>    Rank                              (CompactedDomain) := - Rest_Share;
		attribute<float32>    roundoff_breakpoint                   (ClaimRegion) := - nth_element(
																							Rank
																							, Rest_claim
																							, Region_rel
																						);
		attribute<bool >      above_breakpoint                  (CompactedDomain) := Rest_Share > roundoff_breakpoint[Region_rel];
		attribute<NrPerson>   Population_Rounded_Balanced       (CompactedDomain) := Population_Int + above_breakpoint[NrPerson];
		attribute<NrPerson>   Population                        (CompactedDomain) := Population_Rounded_Balanced, IntegrityCheck = "Final_ClaimRealisation > 0.95f || ClaimRegion/ignore_check_enough_suitable_locations"
		{
			attribute<NrPerson> Population_perRegion                  (ClaimRegion) := sum(Population, Region_rel);
		}
		attribute<NrPerson>           delta_Population            (CompactedDomain) := Population - OldState_Pop/Population;
		attribute<Person_BuiltUpKm2> Population_inRes_Density     (CompactedDomain) := Population_Rounded_Balanced[Person] / BuiltUp_Residential_Area;
		attribute<Person_Km2>        Population_TotalLand_Density (CompactedDomain) := (Population_Rounded_Balanced[Person] / CompactedDomain/area);
		attribute<NrPerson>          Population_sum                   (ClaimRegion) := sum(Population_Rounded_Balanced, Region_rel);
		attribute<float32>           Final_ClaimRealisation           (ClaimRegion) := Population_sum[Person] / Claims/Population;
		
		
		container PopulationDensity_8dir  := Preprocessing/Population_Neighbourhoods/Create_8dir_T(recollect_by_cond(domain/IsCompactedDomain, Population_TotalLand_Density)[float64]);
		
		container Diagnostics 
		{
			unit<uint32> Relevant_ClaimRegions:= select_with_org_rel(pcount(Region_rel) > 1000) 
			{
				attribute<uint32>   ncells                       := pcount(Region_rel)[org_rel];
				attribute<LatLong_Base>  Geometry              (poly) := ClaimRegion/Geometry_LL[org_rel];
				attribute<Person>   Total_Pop_pool               := (Claims/Population - sum(Reduced_Population, Region_rel))[org_rel];
				attribute<Person>   Total_In_NewBuiltup          := sum(Population_in_New_BuiltUp_Area, Region_rel)[org_rel];
				attribute<float32>  Perc_Pop_pool_in_new_builtup := Total_In_NewBuiltup[float32] /  Total_Pop_pool[float32];
				attribute<float32>  Perc_Pop_in_new_builtup      := Total_In_NewBuiltup[float32] /  (Claims/Population[float32])[org_rel];
				attribute<float32>  Perc_New_BuiltUp             := sum(New_BuiltUp_Area[float32], Region_rel)[org_rel] / sum(BuiltUp_Total_Area[float32], Region_rel)[org_rel];
			}
		}
		
		
		
		//adhoc 
		container Analyse_tbv_newBU_densities 
		{
			// attribute<Person>            PrevState_Pop                                   (CompactedDomain) := OldState_Pop/Population[Person];
			// attribute<Person>            NewState_Pop                                    (CompactedDomain) := Population_Rounded_Balanced[Person];
			attribute<BuiltUpKm2>        PrevState_BUres                                 (CompactedDomain) := OldState_BuiltUp/BuiltUp_Residential_Area; 
			attribute<BuiltUpKm2>        NewState_BUres                                  (CompactedDomain) := NewState_BuiltUp/BuiltUp_Residential_Area; 
			// attribute<Km2>               Area                                            (CompactedDomain) := CompactedDomain/area;
			// attribute<Suit>              Suitability_NonNegative                         (CompactedDomain) := Suitability/Population;
			
			attribute<BuiltUpKm2>        BUres_Y1990                                     (CompactedDomain) := (SourceData/BuiltUp/Residential/Y1990[km2] / 1[km2] * 1[BuiltUpKm2])[CompactedDomain/org_rel];
			attribute<BuiltUpKm2>        BUres_Y2020                                     (CompactedDomain) := OldState_BuiltUp/BuiltUp_Residential_Area; 
			attribute<BuiltUpKm2>        BUres_Y2030                                     (CompactedDomain) := NewState_BuiltUp/BuiltUp_Residential_Area; 
			
			attribute<Person>            Pop_Y1990_org                                   (CompactedDomain) := OldState_Pop/Y1990_pop/Population_org[Person];
			attribute<Person>            Pop_Y1990                                       (CompactedDomain) := OldState_Pop/Y1990_pop/Population[Person];
			attribute<Person>            Pop_Y2020                                       (CompactedDomain) := OldState_Pop/Population[Person];
			attribute<Person>            Pop_Y2030                                       (CompactedDomain) := Population_Rounded_Balanced[Person];
			
			attribute<BuiltUPKm2>        BU_Added                                        (CompactedDomain) := NewState_BUres - PrevState_BUres;
			
			// attribute<Person>            Pop_Reducted_Proportional                       (CompactedDomain) := Proportional_Reducted_Population - PrevState_Pop; 
			// attribute<Person>            Pop_Reducted_Absolute                           (CompactedDomain) := Reduced_Population - Proportional_Reducted_Population; 
			// attribute<Person>            Pop_Added_To_New_BU                             (CompactedDomain) := Population_in_New_BuiltUp_Area; 
			// attribute<Person>            Pop_Added_To_Existing_BU_Cell_InAlloc           (CompactedDomain) := PrevState_BUres > 0f ? NewState_Pop - Population_beforeAllocation : 0[Person]; 
			// attribute<Person>            Pop_Added_To_CompletelyNew_BU_Cell_InAlloc      (CompactedDomain) := PrevState_BUres == 0f ? NewState_Pop - Population_beforeAllocation : 0[Person]; 
			
			
			// attribute<float32>           Fraction_of_cell_that_is_NewBU                  (CompactedDomain) := NewState_BuiltUp/Builtup_Residential_Share - OldState_BuiltUp/Builtup_Residential_Share;
			// attribute<bool>              NewBU_fraction_0_00001pr                        (CompactedDomain) := Fraction_of_cell_that_is_NewBU >  0.0f     && Fraction_of_cell_that_is_NewBU <= 0.0001f;
			// attribute<bool>              NewBU_fraction_00001_1pr                        (CompactedDomain) := Fraction_of_cell_that_is_NewBU >  0.0001f  && Fraction_of_cell_that_is_NewBU <= 0.01f;
			// attribute<bool>              NewBU_fraction_1_5pr                            (CompactedDomain) := Fraction_of_cell_that_is_NewBU >  0.01f  && Fraction_of_cell_that_is_NewBU <= 0.05f;
			// attribute<bool>              NewBU_fraction_5_25pr                           (CompactedDomain) := Fraction_of_cell_that_is_NewBU >  0.05f  && Fraction_of_cell_that_is_NewBU <= 0.25f; 
			
			// attribute<Person_km2>        Density_on_land_PrevState                       (CompactedDomain) := PrevState_Pop > 0f ? PrevState_Pop / Area : null_f;
			// attribute<Person_km2>        Density_on_land_NewState                        (CompactedDomain) := NewState_Pop > 0f ? NewState_Pop / Area : null_f;
			
			// attribute<Person_km2>        Density_on_land_of_Totalpop_0_1prNewBU          (CompactedDomain) := NewBU_fraction_0_1pr ? NewState_Pop / Area : null_f,  Descr = "people added in step 3 to new BU divided by land area in the cell. So this is lower, because the cell could already have some population.";
			// attribute<Person_km2>        Density_on_land_of_Totalpop_1_5prNewBU          (CompactedDomain) := NewBU_fraction_1_5pr ? NewState_Pop / Area : null_f,  Descr = "people added in step 3 to new BU divided by land area in the cell. So this is lower, because the cell could already have some population.";
			
			attribute<bool>                 CellOfInterestForExport                         (CompactedDomain) := OR(IsDefined(BUres_Y1990), IsDefined(BUres_Y2020),  IsDefined(BUres_Y2030),  IsDefined(Pop_Y1990_org),  IsDefined(Pop_Y1990),  IsDefined(Pop_Y2020),   IsDefined(Pop_Y2030),  IsDefined(BU_Added));
		
			unit<uint32> Export_Chris := select_with_org_rel(CellOfInterestForExport)
			// , StorageName = "='%LocalDataProjDir%/Indicators/'+ModelParameters/StudyArea+'/Analyse_tbv_newBU_densities_'+ModelParameters/StudyArea+ModelParameters/FileSuffix+'.csv'", StorageType = "str"
			{
				attribute<BuiltUpKm2>        BUres_Y1990                                      := MakeDefined(../BUres_Y1990[org_rel], 0[BuiltUpKm2]);
				attribute<BuiltUpKm2>        BUres_Y2020                                      := MakeDefined(../BUres_Y2020[org_rel], 0[BuiltUpKm2]);
				attribute<BuiltUpKm2>        BUres_Y2030                                      := MakeDefined(../BUres_Y2030[org_rel], 0[BuiltUpKm2]);
				
				attribute<Person>            Pop_Y1990_org                                    := MakeDefined(../Pop_Y1990_org[org_rel], 0[Person]);
				attribute<Person>            Pop_Y1990                                        := MakeDefined(../Pop_Y1990[org_rel], 0[Person]);
				attribute<Person>            Pop_Y2020                                        := MakeDefined(../Pop_Y2020[org_rel], 0[Person]);
				attribute<Person>            Pop_Y2030                                        := MakeDefined(../Pop_Y2030[org_rel], 0[Person]);
				
				attribute<BuiltUPKm2>        BU_Added                                         := MakeDefined(../BUres_Y1990[org_rel], 0[BuiltUpKm2]);
				attribute<CompactedDomain>   org_rel : DisableStorage = "TRUE";
			}
		
		
			unit<uint32> PerRegion := ClaimRegion
			{
				attribute<Person>            Pop_Reducted_Proportional                       := sum(../Pop_Reducted_Proportional, Region_rel);
				attribute<Person>            Pop_Reducted_Absolute                           := sum(../Pop_Reducted_Absolute, Region_rel);
				attribute<Person>            Pop_Added_To_New_BU                             := sum(../Pop_Added_To_New_BU, Region_rel);
				attribute<Person>            Pop_Added_To_Existing_BU_Cell_InAlloc           := sum(../Pop_Added_To_Existing_BU_Cell_InAlloc, Region_rel);
				attribute<Person>            Pop_Added_To_CompletelyNew_BU_Cell_InAlloc      := sum(../Pop_Added_To_CompletelyNew_BU_Cell_InAlloc, Region_rel);
				
				attribute<Person_km2>        Density_on_land_PrevState                       := mean(../Density_on_land_PrevState, Region_rel);
				attribute<Person_km2>        Density_on_land_NewState                        := mean(../Density_on_land_NewState, Region_rel);
				 
				attribute<uint32>            Count_NewBU_fraction_0_00001pr_cells            := sum_uint32(NewBU_fraction_0_00001pr, Region_rel);
				attribute<uint32>            Count_NewBU_fraction_00001_1pr_cells            := sum_uint32(NewBU_fraction_00001_1pr, Region_rel);
				attribute<uint32>            Count_NewBU_fraction_1_5pr_cells                := sum_uint32(NewBU_fraction_1_5pr, Region_rel);
				attribute<uint32>            Count_NewBU_fraction_5_25pr_cells               := sum_uint32(NewBU_fraction_5_25pr, Region_rel);
				
				// attribute<Person_km2>        Density_on_land_of_Totalpop_0_1prNewBU          := mean(../Density_on_land_of_Totalpop_0_1prNewBU, Region_rel);
				// attribute<Person_km2>        Density_on_land_of_Totalpop_1_5prNewBU          := mean(../Density_on_land_of_Totalpop_1_5prNewBU, Region_rel);
			}
		}
	}
}